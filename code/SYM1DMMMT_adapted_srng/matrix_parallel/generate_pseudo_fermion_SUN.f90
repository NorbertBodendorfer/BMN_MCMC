!the traceless condition sum_i phi_ii= sum_i pf_ii=0 is taken into account. 
!info=0 -> OK
!info=1 -> err in CG solver (did not converge)
SUBROUTINE generate_pseudo_fermion_SUN(nbc,acoeff_pf,bcoeff_pf,temperature,&
     &xmat,alpha,pf,GAMMA10d,max_err,max_iteration,iteration,myrank,&
     &nbmn,flux,info)
  
  !use mtmod !Mersenne twistor
  implicit none
  
  include 'mpif.h'
  include 'size_parallel.h'
  !***** input *****
  integer nbc,nbmn
  integer myrank
  double precision temperature,flux
  double complex xmat(1:nmat_block,1:nmat_block,1:ndim,&
       &-(nmargin-1):nsite_local+nmargin)
  double precision alpha(1:nmat_block*nblock)
  double complex GAMMA10d(1:ndim,1:nspin,1:nspin)
  integer max_iteration
  double precision acoeff_pf(0:nremez_pf)
  double precision bcoeff_pf(1:nremez_pf)!bcoeff(1) is the smallest. 
  double precision max_err
  !***** output *****
  double complex pf(1:nmat_block,1:nmat_block,1:nspin,&
       &-(nmargin-1):nsite_local+nmargin)
  integer iteration,info
  !***** For MPI *****
  integer IERR
  !*******************
  !pseudo fermion; phi is generated by Gaussian weight, pf=D^{-1/8}*phi
  double complex phi(1:nmat_block,1:nmat_block,1:nspin,&
       &-(nmargin-1):nsite_local+nmargin)
  double complex chi(1:nremez_pf,1:nmat_block,1:nmat_block,1:nspin,&
       &-(nmargin-1):nsite_local+nmargin)
  double complex trace(1:nsublat,1:nspin,1:nsite_local)
  double complex trace_local(1:nsublat,1:nspin,1:nsite_local)
  integer imat,jmat
  integer ispin
  integer isite
  integer iremez
  integer iblock,jblock
  integer isublat
  integer i


  double precision r1,r2

  !**************************************************************************
  !**** we must be careful about the normalization of the Gaussian term. ****
  !**************************************************************************
  if(myrank.GT.0)then
     !throw away some random numbers in order to avoid the communication
     do i=1,nmat_block*nmat_block*nspin*nsite_local*myrank
      call BoxMuller(r1,r2)
     end do
  end if
  
  do imat=1,nmat_block     
     do jmat=1,nmat_block
        do ispin=1,nspin
           do isite=1,nsite_local
              call BoxMuller(r1,r2)
              phi(imat,jmat,ispin,isite)=&
                   &(dcmplx(r1)+dcmplx(r2)*(0D0,1D0))/dcmplx(dsqrt(2d0))
           end do
        end do
     end do
  end do
  !****************************
  !*** traceless projection ***
  !****************************
  call who_am_i(myrank,isublat,iblock,jblock)
  trace_local=(0d0,0d0)
  if(iblock.EQ.jblock)then
     do ispin=1,nspin
        do isite=1,nsite_local
           do imat=1,nmat_block
                 trace_local(isublat,ispin,isite)=&
                      &trace_local(isublat,ispin,isite)&
                   &+phi(imat,imat,ispin,isite)
           end do
        end do
     end do
  end if

    call MPI_Allreduce(trace_local,trace,nsublat*nspin*nsite_local,&
       MPI_DOUBLE_COMPLEX,&
       &MPI_SUM,MPI_COMM_WORLD,IERR)

  trace=trace/dcmplx(nmat_block*nblock)
  
  call who_am_i(myrank,isublat,iblock,jblock)
  if(iblock.EQ.jblock)then
     do ispin=1,nspin
        do isite=1,nsite_local
           do imat=1,nmat_block
              phi(imat,imat,ispin,isite)=phi(imat,imat,ispin,isite)&
                &-trace(isublat,ispin,isite)
           end do         
        end do
     end do
  end if
  !******************************
  !*** adjust margin and b.c. ***
  !******************************
  call Adjust_margin_and_bc_pf(phi,myrank,nbc)


  !**************************
  !*** pf = D^{-1/8}*phi  ***
  !**************************

  
  call solver_biCGm(nbc,nremez_pf,bcoeff_pf,temperature,xmat,alpha,phi,chi,&
       GAMMA10d,max_err,max_iteration,iteration,myrank,nbmn,flux,info)

  pf=dcmplx(acoeff_pf(0))*phi

  do iremez=1,nremez_pf
     do imat=1,nmat_block     
        do jmat=1,nmat_block
           do ispin=1,nspin
              do isite=-nimprove,nsite_local+1+nimprove
                 pf(imat,jmat,ispin,isite)=pf(imat,jmat,ispin,isite)&
                      &+dcmplx(acoeff_pf(iremez))&
                      &*chi(iremez,imat,jmat,ispin,isite)
              end do
           end do
        end do
     end do
  end do

  if(myrank.LT.nblock*nblock*nsublat-1)then
     !throw away some random numbers in order to avoid the communication
     do i=1,nmat_block*nmat_block*nspin*nsite_local*&
          &(nblock*nblock*nsublat-myrank-1)
        call BoxMuller(r1,r2)
     end do
  end if

  !test
  !call BoxMuller(r1,r2)
  !write(*,*)myrank,r1,r2

  return
  
END SUBROUTINE Generate_pseudo_fermion_SUN
