!pseudo fermion
!phi is generated by Gaussian weight e^{-Tr(phi^dag*phi)}, 
!pf=D^{-1/8}*phi
!traceless condition sum_i phi_ii= sum_i pf_ii=0 is taken into account. 
SUBROUTINE generate_pseudo_fermion_SUN(pf,xmat,alpha,&
     &GAMMA10d,acoeff_pf,bcoeff_pf,max_err,max_iteration,iteration,&
     &nbc,nbmn,temperature,flux,info)
  
  !use mtmod !Mersenne twistor
  implicit none
  
  include '../staticparameters.f90'
  !***** input *****
  integer nbc,nbmn,info
  double precision temperature,flux
  double complex xmat(1:nmat,1:nmat,1:ndim,-(nmargin-1):nsite+nmargin)
  double precision alpha(1:nmat)
  integer max_iteration
  double precision acoeff_pf(0:nremez_pf)
  double precision bcoeff_pf(1:nremez_pf)!bcoeff(1) is the smallest. 
  double precision max_err
  double complex GAMMA10d(1:ndim,1:nspin,1:nspin)
  !***** output *****
  double complex pf(1:nmat,1:nmat,1:nspin,-(nmargin-1):nsite+nmargin,1:npf)
  integer iteration
  !*********************
  double complex phi(1:nmat,1:nmat,1:nspin,-(nmargin-1):nsite+nmargin,1:npf)
  double complex chi(1:nremez_pf,1:nmat,1:nmat,1:nspin,&
       &-(nmargin-1):nsite+nmargin,1:npf)

  double complex trace
  integer imat,jmat
  integer ispin
  integer isite
  integer iremez
  integer i
  integer ipf




  double precision r1,r2

  !**************************************************************************
  !**** we must be careful about the normalization of the Gaussian term. ****
  !**************************************************************************
  do ipf=1,npf

     do isite=1,nsite
        do ispin=1,nspin
           do jmat=1,nmat
              do imat=1,nmat 
                 call BoxMuller(r1,r2)
                 phi(imat,jmat,ispin,isite,ipf)=&
                      &(dcmplx(r1)+dcmplx(r2)*(0D0,1D0))/dcmplx(dsqrt(2d0))
              end do
           end do
        end do
     end do
     
     !****************************
     !*** traceless projection ***
     !****************************
     do isite=1,nsite
        do ispin=1,nspin
           trace=(0d0,0d0)
           do imat=1,nmat
              trace=trace+phi(imat,imat,ispin,isite,ipf)
           end do
           trace=trace/dcmplx(nmat)
           do imat=1,nmat
              phi(imat,imat,ispin,isite,ipf)=phi(imat,imat,ispin,isite,ipf)-trace
           end do
        end do
     end do
  end do
  !*********************************
  !*** adjust the margin and b.c.***
  !*********************************
  call Adjust_margin_and_bc_pf(phi,nbc)
  !**************************
  !*** pf = D^{-1/8}*phi  ***
  !************************** 
  call solver_biCGm(nbc,nbmn,nremez_pf,&
       xmat,alpha,phi,chi,GAMMA10d,&
       bcoeff_pf,max_err,max_iteration,iteration,&
       temperature,flux,info)
  if(rhmc_verbose.EQ.1) then
      print*, "pseudoferm generation  dev chi ", Sum(chi)
      print*, "pseudoferm generation  dev phi ", Sum(phi)
  end if
  do ipf=1,npf
     do isite=-(nmargin-1),nsite+nmargin
        do ispin=1,nspin
           do jmat=1,nmat
              do imat=1,nmat 
                 pf(imat,jmat,ispin,isite,ipf)=&
                      dcmplx(acoeff_pf(0))*phi(imat,jmat,ispin,isite,ipf)
              end do
           end do
        end do
     end do
     do iremez=1,nremez_pf
        do isite=-(nmargin-1),nsite+nmargin
           do ispin=1,nspin
              do jmat=1,nmat
                 do imat=1,nmat     
                    
                    pf(imat,jmat,ispin,isite,ipf)=pf(imat,jmat,ispin,isite,ipf)&
                         &+dcmplx(acoeff_pf(iremez))&
                         &*chi(iremez,imat,jmat,ispin,isite,ipf)
                 end do
              end do
           end do
        end do
     end do
  end do

  return
  
END SUBROUTINE Generate_pseudo_fermion_SUN
